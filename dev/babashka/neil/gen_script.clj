(ns babashka.neil.gen-script
  (:require [clojure.edn :as edn]
            [clojure.string :as str]))

(def version-str
  (-> (edn/read-string (slurp "deps.edn"))
      (get-in [:aliases :neil :project :version])))

(def meta-template
  `[(~'ns ~'babashka.neil.meta)
    (~'def ~'version
      "This def was generated by the neil build script."
      ~version-str)])

(def meta-str
  (str/join "\n" (map pr-str meta-template)))

(defn gen-script []
  (let [prelude (slurp "prelude")
        curl (slurp "src/babashka/neil/curl.clj")
        git (slurp "src/babashka/neil/git.clj")
        new (slurp "src/babashka/neil/new.clj")
        test (slurp "src/babashka/neil/test.cljc")
        project (slurp "src/babashka/neil/project.clj")
        rewrite (slurp "src/babashka/neil/rewrite.clj")
        neil (slurp "src/babashka/neil.clj")]
    (spit "neil" (str/join "\n" [prelude meta-str
                                 curl git rewrite project new test
                                 neil]))))
